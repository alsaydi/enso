
start Document

Document ::= [Document] defs:Def*

Def ::= Device 
     | Context 
     | Controller 
     | Action 
     | Struct 
     | Enum

Device ::= [Device] "device" name:sym ("extends" super:sym)? "{" elements:DeviceElt* "}"

DeviceElt ::= Attribute 
	   | Source 
	   | ActionImpl

Context ::= [Context] "context" name:sym "as" type:Type Indices? "{" elements:ContextElt* "}"

ContextElt ::= SourceRef | ContextRef

Controller ::= [Controller] "controller" name:sym "{" elements:ControllerElt* "}"

ControllerElt ::= ContextRef | ActionRef

Action ::= [Action] "action" name:sym "{" actions:ActionDecl+ "}"

ActionDecl ::= [ActionDecl] name:sym "(" variables:{Variable ","}* ")" ";"

Struct ::= [Struct] "structure" name:sym "{" variables:Variable* "}"

Variable ::= [Variable] name:sym "as" type:Type

Enum ::= [Enum] "enumeration" name:sym "{" values:{sym ","}* "}"

Attribute ::= [Attribute] "attribute" variable:Variable ";"

Source ::= [Source] "source" name:sym "as" type:Type Indices? ";"

ActionImpl ::= [ActionImpl] "action" name:sym ";"

Indices ::= "indexed" "by" indices:{Variable ","}+

SourceRef ::= [SourceRef] "source" name:sym "from" device:Device^ ";"
ContextRef ::= [ContextRef] "context" context:Context^ ";"
ActionRef ::= [ActionRef] "action" action:Action^ "on" device:Device^ ";"

Type ::= Primitive
	     | [Class] name:sym
	     | [Array] type:Type "[" "]"
 
Primitive ::= [Primitive] "Integer" {kind = "Integer"}
	   |  [Primitive] "Boolean" {kind = "Boolean"}
	   |  [Primitive] "String" {kind = "String"}
	   |  [Primitive] "Float" {kind = "Float"}

