


class System
! elements: Element*
! sensors: Sensor*
end

class Pipe
  input: OutputElement
  output: InputElement?
  diameter: real
  length: real
  // computed
  area: real = "@diameter**2 * 3.14/4"
  volume: real = "@area * @length"
end

class Element 
  #name: str
end

class OutputElement < Element
! outputs: Pipe* / input
  output:Pipe = "@outputs[0]"
end
class InputElement < Element
  inputs: Pipe* / output
  input: Pipe = "@inputs[0]"
end

class Joint < OutputElement, InputElement
end

class Source < OutputElement
  kind: str
end

class Exhaust < InputElement
end

class Burner < OutputElement, InputElement
  gas: Pipe
end

class Radiator < OutputElement, InputElement
end

class Vessel < OutputElement, InputElement
  capacity: real
end

class Valve < OutputElement, InputElement
end

class Thermostat < Element
end

class Splitter < InputElement, OutputElement
  left:Pipe = "@outputs[0]"
  right:Pipe = "@outputs[1]"
end

class Pump < OutputElement, InputElement
end

class Sensor < Element, InputElement
  kind: str // temperature, pressure, flow
end

primitive str
primitive int
primitive bool
primitive real
