

def datatable(rows, columns: column(label, view[row])) {
    <table style="border-width: 1px; border-collapse: collapse;">
    <thead>
    <tr>
    for (c: columns) {
      <th>$c->label</th>
    }
    </tr>
    </thead>
    <tbody>
    for (row: rows) {
      <tr>
    	for (c: columns) {
	  <td>
    	  c->view(row);
	   </td>
	}
      </tr>
    }
    </tbody>
    </table>
}


def html(title, yield[]) {
  <html>
    <head><title>$title</title></head>
    <body>yield();</body>
  </html>
}


def navigate(title, page) {
  <a href=page>$title</a>
}

def textedit(data) {
  let
    key = address(data)
  in {
    if (errors[key]) {
      <span style="color: red;">$errors[key]</span>
    }
    <input name=key type="text" value=data/>
  }
}

def crud(obj, return) {
  form {
    table {
      for (f: obj->schema_class->fields) {
        tr {
          td { $obj[f->name] ": " }
          td { edit_field(f, obj[f->name]); }
        }
      }
    }
    submit("Submit", return);
  }
}

def edit_field(owner, field, data) {
  switch (field->type) {
    case Primitive: {
       if (field->type->name == "bool")
         checkbox(data);
       else 
         textedit(data);
    }
    case Class: {
       if (field->many)
         select(data, owner[field->name]);
       else
         select1(data, owner[field->name]);
    }
  }
}


def select1(elt, list) {
  <select name=address(elt)>
   for (x: list) 
     if (x == elt)
       <option selected="true"
              value=address(x)> $x->name </option>
     else
       <option value=address(x)> $x->name </option>
  </select>   
}

def select(elts, list) {
  <select multiple="true" name=address(elts) + "[]">
   for (x: list) 
     if (x in elts)  
       <option selected="true" 
       	       value=address(x)> $x->name </option>
     else
       <option value=address(x)> $x->name </option>
  </select>   
}

def checkbox(data) {
  // deal with unchecked value
  <input type="hidden" name=address(data) value="false"/>
  if (data)
    <input name=address(data) type="checkbox" checked="true" value="true"/>
  else
    <input name=address(data) type="checkbox" value="true"/>
}

def form(yield[]) {
  <form action=self method="post">
    yield();
  </form>
}


def button(text, action) {
  let 
    name = gensym
  in {
     <input type="submit" name=name value=text /> 
     doif name action;
  }
}

def submit(text, url) {
  button(text, submit_action(url));
}


def delete(x, url) {
  button("Delete", delete_action(x, url));
}

def delete_checkbox(x) {
  let
    name = gensym
  in {
    <input type="checkbox" name=name value="true"/>
    doif name check_delete_action(x);
  }
}
