//columns: column(label, view)*
//menu: item(title, link) | menu(name, subs: menu*) 

start Web

Web ::= [Web] defs:Def*

Stat ::= [For] "for" "(" var:sym ":" iter:Exp Index? ")" body:Stat
       | [If] "if" "(" cond:Exp ")" body:Stat Else?
       | Call ";"
       | Call body:Stat
       | [Element] "<" tag:sym attrs:Attr* "/>"
       | [Element] "<" tag:sym attrs:Attr*  ">" body:Stat* "</" sym ">"
       | [Block] "{" stats:Stat* "}"
       | [Text] value:str
       | [Let] "let" decls:Assign* "in" body:Stat
       | [Output] "output" "(" exp:Exp ")"
       | [Yield] "yield" 

Call ::= [Call] func:sym Args?


Def ::= [Def] "def" name:sym Sig? body:Stat


Exp ::= [Field] exp:Exp "." name:sym
      | [Var] name:sym
      | [Str] value:str
      | Call
      | [Link] "link" "(" exp:Exp ")"

      
Index ::= "," index:sym
Assign ::= [Assign] name:sym "=" exp:Exp

Else ::=  "else" else:Stat

Attr ::=  [Attr] name:sym "=" exp:Exp

Args ::= "(" args:{Exp ","}* Nameds? ")"
Nameds ::= "," NamedArgs
NamedArgs ::= named_args:{Named ","}*
Named ::= [Named] name:sym ":" exp:Exp

Sig ::=  "(" formals:{Formal "," }* ")"

Formal ::= name:sym ":" Spec
        |  name:sym

Spec ::= patterns:{Pattern "|"}+

Pattern ::= [Pattern] name:sym "(" args:{Formal ","}+ ")"
	 |  [Pattern] name:sym
	 |  Pattern many:"*"?


