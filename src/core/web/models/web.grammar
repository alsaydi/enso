//columns: column(label, view)*
//menu: item(title, link) | menu(name, subs: menu*) 

start Web

Web ::= [Web] defs:Def*

// TODO: we have a dangling else problem...

Stat ::= [For] "for" "(" var:sym ":" iter:Exp Index? ")" body:Stat
       | [If] "if" "(" cond:Exp ")" body:Stat ("else" else:Stat)?
       | [Call] func:sym Args? block:Stat
       | Call ";" 
       | [Element] "<" tag:sym attrs:Attr* "/>"
       | [Element] "<" tag:sym attrs:Attr*  ">" body:Stat* "</" sym ">"
       | [Block] "{" stats:Stat* "}"
       | [Let] "let" decls:Assign* "in" body:Stat
       | [Output] "$" exp:Exp
       | [Text] value:str
       | [Yield] "yield" 
       | [Switch] "switch" "(" exp:Exp ")" "{" Case*
       	 	  	   ("default" default:Stat)? "}"

Call ::= [Call] func:sym Args

Case ::= [Case] "case" name:sym ":" body:Stat

Def ::= [Def] "def" name:sym Sig? body:Stat


Exp ::= [Field] exp:Exp "->" name:sym
      | [Var] name:sym
      | [Str] value:str
      | [Int] value:int
      | Call  
      | [Link] "link" "(" exp:Exp ")"
      | [Address] "address" "(" exp:Exp ")"
      | [GenSym] "gensym"
      | [Concat] lhs:Exp "+" rhs:Exp  // TODO: precedences
      | [Equal] lhs:Exp "==" rhs:Exp 
      | [In] lhs:Exp "in" rhs:Exp 

      
Index ::= "," index:sym
Assign ::= [Assign] name:sym "=" exp:Exp

Attr ::=  [Attr] name:sym "=" exp:Exp

Args ::= "(" args:{Exp ","}* Nameds? ")"
Nameds ::= "," NamedArgs
NamedArgs ::= named_args:{Named ","}*
Named ::= [Named] name:sym ":" exp:Exp

Sig ::=  "(" formals:{Formal "," }* ")"

Formal ::= [Formal] name:sym ":" Spec
        |  [Formal] name:sym

Spec ::= patterns:{Pattern "|"}+

Pattern ::= [Pattern] name:sym "(" args:{Formal ","}+ ")"
	 |  [Pattern] name:sym
	 |  Pattern many:"*"?


