diagram(schema)

graph [font.size=12,fill.color=(255,255,255)] {
  // create the nodes (one for each class)
  for (class : schema@classes)
   label ClassBox[class]   // declare the "identifier" of this node is a class
     box [line.width=3, fill.color=(255,228,181)] {  // this is the class rectangle
       vertical {
          text [font.size=16,font.weight=700] class@name
          for (field : class@defined_fields)
            if (field@computed == nil)
             if (field@type is Primitive)  // TODO: NEED ?
               horizontal {
                 text field@name  // editable field name
                 text ": "  // editable field name
                 text field@type@name  // TODO: drop-down for primitive types
               }
       }
    }
  // create the subclass links
  for (class : schema@classes)
    for (super : class@supers)
      connector [line.width=3,line.color=(255,0,0)] (ClassBox[class] --> ClassBox[super])
  
  // create the relationship edges that go between nodes
  for (class : schema@classes)
    for (field : class@defined_fields)
     if (field@computed == nil)
      if (!(field@type is Primitive) & (field@inverse == nil | field@_id < field@inverse@_id))
        connector (field@name, (field@inverse==nil ? "" : field@inverse@many ? "*" : field@inverse@optional ? "?" : "1") : ClassBox[field@owner]   
                  -- ClassBox[field@type] : field@inverse@name, (field@many ? "*" : field@optional ? "?" : "1") )
 }